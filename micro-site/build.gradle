apply from: "$rootDir/gradle/node.gradle"

dependencies {

    // SpringBoot Projects
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    developmentOnly("org.springframework.boot:spring-boot-devtools")

    // springdoc-openapi-ui
    implementation("org.springdoc:springdoc-openapi-ui:${springDocVersion}")

    // lombok
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

jar {
    archiveBaseName = "micro-site"
    archiveVersion = "$catCafeVersion"
    enabled = true
}

bootJar {
    enabled = true
    launchScript()
}

bootRun {
    sourceResources sourceSets.main
}

node {
    version = "16.4.2"
    npmVersion = "7.19.1"
    download = true
    workDir = file("${project.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${project.projectDir}/.gradle/npm")
    nodeModulesDir = file("${project.projectDir}/src/main/riot")
}

task('npm_run_generate-client').dependsOn ":openapi-generate:openApiGenerate"
compileJava.dependsOn tasks.generateClient, tasks.webpackDevelopment