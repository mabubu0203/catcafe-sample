apply from: "$rootDir/gradle/apigenerate.gradle"
apply plugin: "com.google.cloud.tools.jib"

dependencies {

    // SpringBoot Projects
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    runtimeOnly("org.springframework.boot:spring-boot-properties-migrator")

    // openapi generator
    compileOnly("io.swagger:swagger-annotations:${swaggerAnnotationsVersion}")
    compileOnly("io.springfox:springfox-core:3.0.0")
    compileOnly("org.openapitools:jackson-databind-nullable:0.2.1")

    // springdoc-openapi-ui
    implementation("org.springdoc:springdoc-openapi-webflux-ui:${springDocVersion}")
    implementation("org.springdoc:springdoc-openapi-security:${springDocVersion}")

    // lombok
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

    // mysql
    runtimeOnly("mysql:mysql-connector-java")
}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

jar {
    archiveBaseName = "micro-api"
    enabled = true
    manifest {
        attributes 'Main-Class': 'mabubu0203.com.github.catcafe.api.ApiApp'
    }
}

bootJar {
    enabled = true
    archiveBaseName = "micro-api"
    archiveVersion = "$catCafeVersion"
    launchScript()
}

jib {
    from {
        image = "adoptopenjdk/openjdk11"
    }
    to {
        image = "play-with-jib/micro-api"
    }
}

compileJava.dependsOn tasks.openApiValidate, tasks.openApiGenerate
sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"